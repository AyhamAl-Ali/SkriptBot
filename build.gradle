plugins {
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    //Spotbugs
    id "com.github.spotbugs" version "4.7.0"
}

group 'software.bigbade'
version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

lombok {
    version = '1.18.18'
    sha256 = ''
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    //Annotations
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    //json.simple
    implementation 'com.github.cliftonlabs:json-simple:3.1.0'
    //needed to fix lombok bug
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    //JDA for Discord bots
    implementation('net.dv8tion:JDA:4.2.0_247') {
        //noinspection GroovyAssignabilityCheck
        exclude group: 'org.jetbrains', module: 'annotations'
        //noinspection GroovyAssignabilityCheck
        exclude group: 'com.google.code.gson', module: 'gson'
        //noinspection GroovyAssignabilityCheck
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        //noinspection GroovyAssignabilityCheck
        exclude group: 'net.sf.trove4j', module: 'trove4j'
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    //Annotations
    testImplementation 'com.google.code.findbugs:jsr305:3.0.2'
}

test {
    useJUnitPlatform()
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        //noinspection GroovyAssignabilityCheck
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    //noinspection GrUnresolvedAccess
    project*.test
}

shadowJar {
    archiveClassifier.set('')

    minimize()
}

//noinspection GrUnresolvedAccess
build.finalizedBy(shadowJar)

jar {
    manifest {
        attributes 'Main-Class': "com.bigbade.skriptbot.Main"
    }
}
